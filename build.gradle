buildscript {
    ext.kotlin_version = "1.2.50"

    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
    }
}

apply plugin: "forge"
apply plugin: "kotlin"

group "me.glorantq.aboe.chat"
version "2.0.0"
archivesBaseName = "aboe-chat"

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "eclipse"
}

ext {
    json_version = "1.1.1"
    lang3_version = "3.7"
    st4_version = "4.0.8"
    jsonio_version = "4.10.0"
    fe_version = "1.4.4-SNAPSHOT"
}

repositories {
    mavenCentral()

    maven { url "http://repo.spongepowered.org/maven" }
    maven { url "http://maven.sk89q.com/repo/"}
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    embed "org.jetbrains.kotlin:kotlin-reflect"
    embed "com.googlecode.json-simple:json-simple:$json_version"
    embed "org.antlr:ST4:$st4_version"
    embed "com.cedarsoftware:json-io:$jsonio_version"

    compile "com.forgeessentials:forgeessentials:$fe_version:dev"
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
    appendix = "${minecraft.version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand "version": project.version, "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}